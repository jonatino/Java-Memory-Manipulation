buildscript {
    ext.your_github_name = 'Jonatino'
    ext.your_name = 'Jonathan Beaudoin'
    ext.your_email = 'jonathanbeaudoin1996@gmail.com'

    ext.project_name = 'Java-Memory-Manipulation'
    ext.project_description = 'User friendly, Garbage-free, and cross-platform process, module and memory interfacing via the power of Java'
    ext.project_group = 'com.beaudoin.jmm'
    ext.project_artifact = 'Java-Memory-Manipulation'
    ext.project_version = '1.6.3'

    ext.jna_version = '4.3.0-SNAPSHOT'

    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group project_group
version project_version

allprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project_group
            artifactId project_artifact
            version project_version
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project_name
                    description project_description
                    url "https://github.com/$your_github_name/$project_name"
                    scm {
                        url "https://github.com/$your_github_name/$project_name"
                        connection "scm:git:git://github.com/$your_github_name/${project_name}.git"
                        developerConnection "scm:git:ssh:git@github.com:$your_github_name/${project_name}.git"
                    }
                    licenses {
                        license {
                            name 'The GNU General Public License, Version 3.0'
                            url 'https://raw.githubusercontent.com/$your_github_name/$project_name/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id your_github_name
                            name your_name
                            email your_email
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project_name
        licenses = ['GPL-3.0']
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/$your_github_name/$project_name"
        issueTrackerUrl = "https://github.com/$your_github_name/$project_name/issues"
        vcsUrl = "https://github.com/$your_github_name/${project_name}.git"
        githubRepo = "$your_github_name/$project_name"
        version {
            name = project_version
            vcsTag = project_version
            gpg {
                sign = true
            }
        }
    }
}

repositories {
    mavenCentral()

    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile "net.openhft:zero-allocation-hashing:0.6"
    compile "net.java.dev.jna:jna:$jna_version"
    compile "net.java.dev.jna:jna-platform:$jna_version"
}